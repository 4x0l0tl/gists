= Network and Data Center Management

This interactive Neo4j graph tutorial covers Network and Data Center Management scenarios.

== Introduction to Problem

Explain the problem here. Be specific.

== Explanation of Scenario

Explain the scenario you are about to cover.

== Explanation of Solution

Explain the Neo4j solution to the scenario as a brief summary.

== Network Dependency Graph

image::http://raw.github.com/neo4j-contrib/gists/master/other/images/datacenter-management-1.PNG[Network Dependency Graph]

== Cypher Query Setup

//setup
//hide
[source,cypher]
----
// Create CRM
CREATE (crm1:CRM { 
			ip:'10.10.32.1', 
			host:'crm-host'
		}) 

// Create ERP
CREATE (erp1:ERP { 
			ip:'10.10.33.1', 
			host:'erp-host'
		}) 

// Create Data Warehouse
CREATE (datawarehouse1:DataWarehouse { 
			ip:'10.10.34.1', 
			host:'datawarehouse-host'
		}) 

// Create Public Website 1
CREATE (publicwebsite1:PublicWebsite { 
			ip:'10.10.35.1', 
			host:'global.acme.com'
		}) 

// Create Public Website 2
CREATE (publicwebsite2:PublicWebsite { 
			ip:'10.10.35.2', 
			host:'support.acme.com'
		}) 

// Create Public Website 3
CREATE (publicwebsite3:PublicWebsite { 
			ip:'10.10.35.3', 
			host:'shop.acme.com'
		}) 

// Create Public Website 4
CREATE (publicwebsite4:PublicWebsite { 
			ip:'10.10.35.4', 
			host:'training.acme.com'
		}) 

// Create Public Website 5
CREATE (publicwebsite5:PublicWebsite { 
			ip:'10.10.35.1', 
			host:'partners.acme.com'
		}) 

// Create Internal Website 1
CREATE (internalwebsite1:InternalWebsite { 
			ip:'10.10.35.2', 
			host:'events.acme.net'
		}) 

// Create Internal Website 2
CREATE (internalwebsite2:InternalWebsite { 
			ip:'10.10.35.3', 
			host:'intranet.acme.net'
		}) 

// Create Internal Website 3
CREATE (internalwebsite3:InternalWebsite { 
			ip:'10.10.35.4', 
			host:'humanresources.acme.net'
		}) 

// Create Webserver VM 1
CREATE (webservervm1:WebserverVM { 
			ip:'10.10.35.5', 
			host:'webserver1-vm-host'
		}) 

// Create Webserver VM 2
CREATE (webservervm2:WebserverVM { 
			ip:'10.10.35.6', 
			host:'webserver2-vm-host'
		}) 

// Create Database VM 1
CREATE (databasevm1:DatabaseVM { 
			ip:'10.10.35.7', 
			host:'database1-vm-host'
		}) 

// Create Database VM 2
CREATE (databasevm2:DatabaseVM { 
			ip:'10.10.35.8', 
			host:'database2-vm-host'
		}) 

// Create Database VM 3
CREATE (databasevm3:DatabaseVM { 
			ip:'10.10.35.9', 
			host:'database3-vm-host'
		}) 

// Create Database VM 4
CREATE (databasevm4:DatabaseVM { 
			ip:'10.10.35.10', 
			host:'database4-vm-host'
		}) 

// Create Hardware 1
CREATE (hardware1:Hardware { 
			ip:'10.10.35.11', 
			host:'hardware1-host'
		}) 

// Create Hardware 2
CREATE (hardware2:Hardware { 
			ip:'10.10.35.12', 
			host:'hardware2-host'
		}) 

// Create Hardware 3
CREATE (hardware3:Hardware { 
			ip:'10.10.35.13', 
			host:'hardware3-host'
		}) 

// Create SAN 1
CREATE (san1:SAN { 
			ip:'10.10.35.14', 
			host:'san-host'
		}) 

// Connect CRM to Database VM 1
CREATE (crm1)-[:DEPENDS_ON]->(databasevm1)

// Connect Public Websites 1-3 to Database VM 1
CREATE 	(publicwebsite1)-[:DEPENDS_ON]->(databasevm1),
	   	(publicwebsite2)-[:DEPENDS_ON]->(databasevm1),
	   	(publicwebsite3)-[:DEPENDS_ON]->(databasevm1)

// Connect Database VM 1 to Hardware 1
CREATE 	(databasevm1)-[:DEPENDS_ON]->(hardware1)

// Connect Hardware 1 to SAN 1
CREATE 	(hardware1)-[:DEPENDS_ON]->(san1)

// Connect Public Websites 1-3 to Webserver VM 1
CREATE 	(webservervm1)<-[:DEPENDS_ON]-(publicwebsite1),
		(webservervm1)<-[:DEPENDS_ON]-(publicwebsite2),
		(webservervm1)<-[:DEPENDS_ON]-(publicwebsite3)

// Connect Internal Websites 1-3 to Webserver VM 1
CREATE 	(webservervm1)<-[:DEPENDS_ON]-(internalwebsite1),
		(webservervm1)<-[:DEPENDS_ON]-(internalwebsite2),
		(webservervm1)<-[:DEPENDS_ON]-(internalwebsite3)

// Connect Webserver VM 1 to Hardware 2
CREATE 	(webservervm1)-[:DEPENDS_ON]->(hardware2)

// Connect Hardware 2 to SAN 1
CREATE 	(hardware2)-[:DEPENDS_ON]->(san1)

// Connect Webserver VM 2 to Hardware 2
CREATE 	(webservervm2)-[:DEPENDS_ON]->(hardware2)

// Connect Public Websites 4-6 to Webserver VM 2
CREATE 	(webservervm2)<-[:DEPENDS_ON]-(publicwebsite4),
		(webservervm2)<-[:DEPENDS_ON]-(publicwebsite5)

// Connect Database VM 2 to Hardware 2
CREATE 	(hardware2)<-[:DEPENDS_ON]-(databasevm2)

// Connect Public Websites 4-5 to Database VM 2
CREATE 	(publicwebsite4)-[:DEPENDS_ON]->(databasevm2),
	   	(publicwebsite5)-[:DEPENDS_ON]->(databasevm2)

// Connect Hardware 3 to SAN 1
CREATE 	(hardware3)-[:DEPENDS_ON]->(san1)

// Connect Database VM 3 to Hardware 3
CREATE 	(hardware3)<-[:DEPENDS_ON]-(databasevm3)

// Connect ERP 1 to Database VM 3
CREATE 	(erp1)-[:DEPENDS_ON]->(databasevm3)

// Connect Database VM 4 to Hardware 3
CREATE 	(hardware3)<-[:DEPENDS_ON]-(databasevm4)

// Connect Data Warehouse 1 to Database VM 4
CREATE 	(datawarehouse1)-[:DEPENDS_ON]->(databasevm4)

RETURN *

----

//graph

=== Overview by Type

Shows a quick overview over the datacenter content grouped by server type.

[source,cypher]
----
MATCH 	(n) 
RETURN 	labels(n)[0] as type,
		count(*) as count, 
		collect(n.host) as names
----

//table

=== Find direct dependencies of all public websites
[source,cypher]
----
MATCH 		(website:PublicWebsite)-[:DEPENDS_ON]->(downstream)
RETURN 		labels(website)[0] + ": " + website.host as Host, 
			collect(labels(downstream)[0] + ": " + downstream.host) as Dependencies
ORDER BY 	Host
----

//table

=== Find direct dependencies of all internal websites
[source,cypher]
----
MATCH 		(website:InternalWebsite)-[:DEPENDS_ON]->(downstream)
RETURN 		labels(website)[0] + ": " + website.host as Host, 
			collect(labels(downstream)[0] + ": " + downstream.host) as Dependencies
ORDER BY 	Host
----

//table

=== Find the most depended-upon component
[source,cypher]
----
MATCH 		(n)<-[:DEPENDS_ON*]-(dependent)
RETURN 		labels(n)[0] + ": " + n.host as Host, 
			count(DISTINCT dependent) AS Dependents
ORDER BY 	Dependents DESC
LIMIT 		1
----

//table

=== Find dependency chain for business critical components:  CRM
[source,cypher]
----
MATCH 		(dependency)<-[:DEPENDS_ON*]-(dependent)
WITH 		dependency, count(DISTINCT dependent) AS Dependents
ORDER BY 	Dependents DESC
LIMIT		1
WITH		dependency
MATCH 		p=(resource:CRM)-[:DEPENDS_ON*]->(dependency)
RETURN 		extract(x in nodes(p) | labels(x)[0] + ": " + x.host) as DependencyChain
----

//table


=== Find dependency chain for business critical components:  ERP
[source,cypher]
----
MATCH 		(dependency)<-[:DEPENDS_ON*]-(dependent)
WITH 		dependency, count(DISTINCT dependent) AS Dependents
ORDER BY 	Dependents DESC
LIMIT		1
WITH		dependency
MATCH 		p=(resource:ERP)-[:DEPENDS_ON*]->(dependency)
RETURN 		extract(x in nodes(p) | labels(x)[0] + ": " + x.host) as DependencyChain
----

//table


=== Find dependency chain for business critical components: Data Warehouse
[source,cypher]
----
MATCH 		(dependency)<-[:DEPENDS_ON*]-(dependent)
WITH 		dependency, count(DISTINCT dependent) AS Dependents
ORDER BY 	Dependents DESC
LIMIT		1
WITH		dependency
MATCH 		p=(resource:DataWarehouse)-[:DEPENDS_ON*]->(dependency)
RETURN 		extract(x in nodes(p) | labels(x)[0] + ": " + x.host) as DependencyChain
----

//table
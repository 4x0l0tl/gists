= Network and Data Center Management

This interactive Neo4j graph tutorial covers Network and Data Center Management scenarios.

== Introduction to Problem

Explain the problem here. Be specific.

== Explanation of Scenario

Explain the scenario you are about to cover.

== Explanation of Solution

Explain the Neo4j solution to the scenario as a brief summary.

== Network Dependency Graph

image::http://raw.github.com/neo4j-contrib/gists/master/other/images/datacenter-management-1.PNG[Network Dependency Graph]

== Cypher Query Setup

//setup
//hide
[source,cypher]
----
// Create CRM
CREATE (crm1:Application { 
			ip:'10.10.32.1', 
			host:'CRM-APPLICATION',
			type: 'APPLICATION',
			system: 'CRM'
		}) 

// Create ERP
CREATE (erp1:Application { 
			ip:'10.10.33.1', 
			host:'ERP-APPLICATION',
			type: 'APPLICATION',
			system: 'ERP'
		}) 

// Create Data Warehouse
CREATE (datawarehouse1:Application { 
			ip:'10.10.34.1', 
			host:'DATA-WAREHOUSE',
			type: 'DATABASE',
			system: 'DW'
		}) 

// Create Public Website 1
CREATE (Internet1:Internet { 
			ip:'10.10.35.1', 
			host:'global.acme.com'
		}) 

// Create Public Website 2
CREATE (Internet2:Internet { 
			ip:'10.10.35.2', 
			host:'support.acme.com'
		}) 

// Create Public Website 3
CREATE (Internet3:Internet { 
			ip:'10.10.35.3', 
			host:'shop.acme.com'
		}) 

// Create Public Website 4
CREATE (Internet4:Internet { 
			ip:'10.10.35.4', 
			host:'training.acme.com'
		}) 

// Create Public Website 5
CREATE (Internet5:Internet { 
			ip:'10.10.35.1', 
			host:'partners.acme.com'
		}) 

// Create Internal Website 1
CREATE (Intranet1:Intranet { 
			ip:'10.10.35.2', 
			host:'events.acme.net'
		}) 

// Create Internal Website 2
CREATE (Intranet2:Intranet { 
			ip:'10.10.35.3', 
			host:'intranet.acme.net'
		}) 

// Create Internal Website 3
CREATE (Intranet3:Intranet { 
			ip:'10.10.35.4', 
			host:'humanresources.acme.net'
		}) 

// Create Webserver VM 1
CREATE (webservervm1:VirtualMachine { 
			ip:'10.10.35.5', 
			host:'WEBSERVER-1'
		}) 

// Create Webserver VM 2
CREATE (webservervm2:VirtualMachine { 
			ip:'10.10.35.6', 
			host:'WEBSERVER-2'
		}) 

// Create Database VM 1
CREATE (customerdatabase1:VirtualMachine { 
			ip:'10.10.35.7', 
			host:'CUSTOMER-DB-1'
		}) 

// Create Database VM 2
CREATE (customerdatabase2:VirtualMachine { 
			ip:'10.10.35.8', 
			host:'CUSTOMER-DB-2'
		}) 

// Create Database VM 3
CREATE (databasevm3:VirtualMachine { 
			ip:'10.10.35.9', 
			host:'ERP-DB'
		}) 

// Create Database VM 4
CREATE (dwdatabase:VirtualMachine { 
			ip:'10.10.35.10', 
			host:'DW-DATABASE'
		}) 

// Create Hardware 1
CREATE (hardware1:Hardware { 
			ip:'10.10.35.11', 
			host:'HARDWARE-SERVER-1'
		}) 

// Create Hardware 2
CREATE (hardware2:Hardware { 
			ip:'10.10.35.12', 
			host:'HARDWARE-SERVER-2'
		}) 

// Create Hardware 3
CREATE (hardware3:Hardware { 
			ip:'10.10.35.13', 
			host:'HARDWARE-SERVER-3'
		}) 

// Create SAN 1
CREATE (san1:Hardware { 
			ip:'10.10.35.14', 
			host:'SAN'
		}) 

// Connect CRM to Database VM 1
CREATE (crm1)-[:DEPENDS_ON]->(customerdatabase1)

// Connect Public Websites 1-3 to Database VM 1
CREATE 	(Internet1)-[:DEPENDS_ON]->(customerdatabase1),
	   	(Internet2)-[:DEPENDS_ON]->(customerdatabase1),
	   	(Internet3)-[:DEPENDS_ON]->(customerdatabase1)

// Connect Database VM 1 to Hardware 1
CREATE 	(customerdatabase1)-[:DEPENDS_ON]->(hardware1)

// Connect Hardware 1 to SAN 1
CREATE 	(hardware1)-[:DEPENDS_ON]->(san1)

// Connect Public Websites 1-3 to Webserver VM 1
CREATE 	(webservervm1)<-[:DEPENDS_ON]-(Internet1),
		(webservervm1)<-[:DEPENDS_ON]-(Internet2),
		(webservervm1)<-[:DEPENDS_ON]-(Internet3)

// Connect Internal Websites 1-3 to Webserver VM 1
CREATE 	(webservervm1)<-[:DEPENDS_ON]-(Intranet1),
		(webservervm1)<-[:DEPENDS_ON]-(Intranet2),
		(webservervm1)<-[:DEPENDS_ON]-(Intranet3)

// Connect Webserver VM 1 to Hardware 2
CREATE 	(webservervm1)-[:DEPENDS_ON]->(hardware2)

// Connect Hardware 2 to SAN 1
CREATE 	(hardware2)-[:DEPENDS_ON]->(san1)

// Connect Webserver VM 2 to Hardware 2
CREATE 	(webservervm2)-[:DEPENDS_ON]->(hardware2)

// Connect Public Websites 4-6 to Webserver VM 2
CREATE 	(webservervm2)<-[:DEPENDS_ON]-(Internet4),
		(webservervm2)<-[:DEPENDS_ON]-(Internet5)

// Connect Database VM 2 to Hardware 2
CREATE 	(hardware2)<-[:DEPENDS_ON]-(customerdatabase2)

// Connect Public Websites 4-5 to Database VM 2
CREATE 	(Internet4)-[:DEPENDS_ON]->(customerdatabase2),
	   	(Internet5)-[:DEPENDS_ON]->(customerdatabase2)

// Connect Hardware 3 to SAN 1
CREATE 	(hardware3)-[:DEPENDS_ON]->(san1)

// Connect Database VM 3 to Hardware 3
CREATE 	(hardware3)<-[:DEPENDS_ON]-(databasevm3)

// Connect ERP 1 to Database VM 3
CREATE 	(erp1)-[:DEPENDS_ON]->(databasevm3)

// Connect Database VM 4 to Hardware 3
CREATE 	(hardware3)<-[:DEPENDS_ON]-(dwdatabase)

// Connect Data Warehouse 1 to Database VM 4
CREATE 	(datawarehouse1)-[:DEPENDS_ON]->(dwdatabase)

RETURN *

----

//graph

=== Overview by Type

Shows a quick overview over the datacenter content grouped by server type.

[source,cypher]
----
MATCH 	(n) 
RETURN 	labels(n)[0] as type,
		count(*) as count, 
		collect(n.host) as names
----

//table

=== Find direct dependencies of all public websites
[source,cypher]
----
MATCH 		(website:Internet)-[:DEPENDS_ON]->(downstream)
RETURN 		website.host as Host, 
			collect(downstream.host) as Dependencies
ORDER BY 	Host
----

//table

=== Find direct dependencies of all internal websites
[source,cypher]
----
MATCH 		(website:Intranet)-[:DEPENDS_ON]->(downstream)
RETURN 		website.host as Host, 
			collect(downstream.host) as Dependencies
ORDER BY 	Host
----

//table

=== Find the most depended-upon component
[source,cypher]
----
MATCH 		(n)<-[:DEPENDS_ON*]-(dependent)
RETURN 		n.host as Host, 
			count(DISTINCT dependent) AS Dependents
ORDER BY 	Dependents DESC
LIMIT 		1
----

//table

=== Find dependency chain for business critical components:  CRM
[source,cypher]
----
MATCH 		(dependency)<-[:DEPENDS_ON*]-(dependent)
WITH 		dependency, count(DISTINCT dependent) AS Dependents
ORDER BY 	Dependents DESC
LIMIT		1
WITH		dependency
MATCH 		p=(resource)-[:DEPENDS_ON*]->(dependency)
WHERE		resource.system = "CRM"
RETURN 		extract(x in nodes(p) | x.host) as DependencyChain
----

//table


=== Find dependency chain for business critical components:  ERP
[source,cypher]
----
MATCH 		(dependency)<-[:DEPENDS_ON*]-(dependent)
WITH 		dependency, count(DISTINCT dependent) AS Dependents
ORDER BY 	Dependents DESC
LIMIT		1
WITH		dependency
MATCH 		p=(resource)-[:DEPENDS_ON*]->(dependency)
WHERE		resource.system = "ERP"
RETURN 		extract(x in nodes(p) | x.host) as DependencyChain
----

//table


=== Find dependency chain for business critical components: Data Warehouse
[source,cypher]
----
MATCH 		(dependency)<-[:DEPENDS_ON*]-(dependent)
WITH 		dependency, count(DISTINCT dependent) AS Dependents
ORDER BY 	Dependents DESC
LIMIT		1
WITH		dependency
MATCH 		p=(resource)-[:DEPENDS_ON*]->(dependency)
WHERE		resource.system = "DW"
RETURN 		extract(x in nodes(p) | x.host) as DependencyChain
----

//table
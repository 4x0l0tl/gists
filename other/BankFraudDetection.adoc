= Bank Fraud Detection
:neo4j-version: 2.0.0-RC1
:author: Kenny Bastani
:twitter: @kennybastani
:tags: domain:finance, use-case:fraud-detection

This interactive Neo4j graph tutorial covers bank fraud detection scenarios.

== Introduction to Problem

Banks and Insurance companies lose billions of dollars every year to fraud. Traditional methods of fraud detection play an important role in minimizing these losses. However increasingly sophisticated fraudsters have developed a variety of ways to elude discovery, both by working together, and by leveraging various other means of constructing false identities. 

== Explanation of Scenario

While no fraud prevention measures can ever be perfect, significant opportunity for improvement lies in looking beyond the individual data points, to the connections that link them. Oftentimes these connections go unnoticed until it is too lateâ€” something that is unfortunate, as these connections oftentimes hold the best clues.

== Explanation of Solution

Graph databases offer new methods of uncovering fraud rings and other sophisticated scams with a high-level of accuracy, and are capable of stopping advanced fraud scenarios in real-time.

== Bank Fraud Data Model

image::https://raw.github.com/neo4j-contrib/gists/master/other/images/BankFraud-1.png[Bank Fraud]

//hide
//setup
[source,cypher]
----
CREATE (accountHolder1:AccountHolder { FirstName: "John", LastName: "Doe", UniqueId: "JohnDoe" }) 
CREATE (accountHolder2:AccountHolder { FirstName: "Jane", LastName: "Appleseed", UniqueId: "JaneAppleseed" }) 
CREATE (accountHolder3:AccountHolder { FirstName: "Matt", LastName: "Smith", UniqueId: "MattSmith" }) 
CREATE (address1:Address { Street: "127 W Ave", City: "San Francisco", State: "California", ZipCode: "94061" })
CREATE (accountHolder1)-[:HAS_ADDRESS]->(address1), (accountHolder2)-[:HAS_ADDRESS]->(address1), (accountHolder3)-[:HAS_ADDRESS]->(address1)
CREATE (phoneNumber1:PhoneNumber { PhoneNumber: "555-555-5555" })
CREATE (accountHolder1)-[:HAS_PHONENUMBER]->(phoneNumber1), (accountHolder2)-[:HAS_PHONENUMBER]->(phoneNumber1)
CREATE (ssn1:SSN { SSN: "241-23-1234" })
CREATE (accountHolder2)-[:HAS_SSN]->(ssn1), (accountHolder3)-[:HAS_SSN]->(ssn1)
CREATE (ssn2:SSN { SSN: "241-23-4567" })<-[:HAS_SSN]-(accountHolder1)
CREATE (creditCard1:CreditCard { AccountNumber: "1234567890123456", Limit: 5000, Balance: 1442.23, ExpirationDate: "01-20", SecurityCode: "123" })<-[:HAS_CREDITCARD]-(accountHolder1)
CREATE (bankAccount1:BankAccount { AccountNumber: "2345678901234567", Balance: 7054.43 })<-[:HAS_BANKACCOUNT]-(accountHolder1)
CREATE (creditCard2:CreditCard { AccountNumber: "1234567890123456", Limit: 4000, Balance: 2345.56, ExpirationDate: "02-20", SecurityCode: "456" })<-[:HAS_CREDITCARD]-(accountHolder2)
CREATE (bankAccount2:BankAccount { AccountNumber: "3456789012345678", Balance: 4231.12 })<-[:HAS_BANKACCOUNT]-(accountHolder2)
CREATE (unsecuredLoan2:UnsecuredLoan { AccountNumber: "4567890123456789-0", Balance: 9045.53, APR: .0541, LoanAmount: 12000.00 })<-[:HAS_UNSECUREDLOAN]-(accountHolder2)
CREATE (bankAccount3:BankAccount { AccountNumber: "4567890123456789", Balance: 12345.45 })<-[:HAS_BANKACCOUNT]-(accountHolder3)
CREATE (unsecuredLoan3:UnsecuredLoan { AccountNumber: "5678901234567890-0", Balance: 16341.95, APR: .0341, LoanAmount: 22000.00 })<-[:HAS_UNSECUREDLOAN]-(accountHolder3)
CREATE (phoneNumber2:PhoneNumber { PhoneNumber: "555-555-1234" })<-[:HAS_PHONENUMBER]-(accountHolder3)
RETURN *
----

//graph
